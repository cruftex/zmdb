// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.2'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

ext {
    min_sdk_version = 16
    compile_sdk_version = 25
    build_tools_version = "24.0.3"
    support_library_version = "25.0.0"
    play_services_version = "9.4.0"
    retrofit_version = "2.1.0"
    picasso_version = "2.5.2"
    gson_version = "2.7"
    android_rate_version = "1.0.1"
    cache2k_version = "0.28-BETA"
    timber_version = "4.3.0"
    eventbus_version = "3.0.0"
    assertj_version = "1.0.0"
    support_test_version = "0.5"
    espresso_version = "2.2.2"
    uiautomator_v18_version = "2.1.2"
    // Robolectric 3.1 needs this dependency in order to test UI
    opengl_api_version = "gl1.1-android-2.1_r1"
    robolectric_version = "3.1.1"
    dexmaker_version = "1.2"
    mockito_version = "1.10.19"
    hamcrest_version = "1.3"
    okhttp_version = "3.4.1"
    conductor_version = "2.0.3"
    mosby_version = "2.0.1"

    /**
     * Base dependencies shared by every sub-module.
     */
    libraries = [
            support_v4 : "com.android.support:support-v4:$support_library_version",
            appcompat_v7 : "com.android.support:appcompat-v7:$support_library_version",
            gridlayout_v7 : "com.android.support:gridlayout-v7:$support_library_version",
            android_design : "com.android.support:design:$support_library_version",
            google_play_services_base : "com.google.android.gms:play-services-base:$play_services_version"
    ]

    /**
     * Commonly referenced third-party libraries.
     */
    thirdPartyLibs = [
            okhttp: "com.squareup.okhttp3:okhttp:$okhttp_version",
            retrofit: "com.squareup.retrofit2:retrofit:$retrofit_version",
            retrofit_converter_gson: "com.squareup.retrofit2:converter-gson:$retrofit_version",
            gson : "com.google.code.gson:gson:$gson_version",
            timber: "com.jakewharton.timber:timber:$timber_version",
            mosby_mvp: "com.hannesdorfmann.mosby:mvp:$mosby_version",
            mosby_viewstate: "com.hannesdorfmann.mosby:viewstate:$mosby_version",
            conductor: "com.bluelinelabs:conductor:$conductor_version",
            conductor_support: "com.bluelinelabs:conductor-support:$conductor_version",
            android_rate: "com.github.hotchemi:android-rate:$android_rate_version",
            eventbus : "org.greenrobot:eventbus:$eventbus_version",
            cache2k_core : "org.cache2k:cache2k-core:$cache2k_version",
            cache2k_api : "org.cache2k:cache2k-core:$cache2k_version",
            picasso: "com.squareup.picasso:picasso:$picasso_version"
    ]

    /**
     * Commonly referenced test libraries
     */
    testLibs = [
            assertj_android: dependencies.create("com.squareup.assertj:assertj-android:${assertj_version}"){
                exclude group: 'com.android.support', module: 'support-annotations'
            },
            assertj_supportv4: dependencies.create("com.squareup" +
                    ".assertj:assertj-android-support-v4:${assertj_version}"){
                exclude group: 'com.android.support', module: 'support-v4'
                exclude group: 'com.android.support', module: 'support-annotations'
            },
            android_junit4_runner: dependencies.create("com.android.support.test:runner:${support_test_version}"){
                //Stripping out current Hamcrest version and replacing with 1.3 declaration
                exclude group: 'org.hamcrest'
                exclude group: 'com.android.support', module: 'support-annotations'
            },
            android_junit4_rules: dependencies.create("com.android.support.test:rules:${support_test_version}"){
                exclude group: 'com.android.support', module: 'support-annotations'
            },
            uiautomator_v18: "com.android.support.test.uiautomator:uiautomator-v18:${uiautomator_v18_version}",
            espresso_core: dependencies.create("com.android.support.test.espresso:espresso-core:${espresso_version}"){
                exclude group: 'com.android.support', module: 'support-annotations'
            },
            espresso_contrib: "com.android.support.test.espresso:espresso-contrib:${espresso_version}",
            espresso_intents: "com.android.support.test.espresso:espresso-intents:${espresso_version}",
            espresso_web: "com.android.support.test.espresso:espresso-web:${espresso_version}",
            junit4: 'junit:junit:4.12',
            opengl_api: "org.khronos:opengl-api:${opengl_api_version}",
            robolectric: dependencies.create("org.robolectric:robolectric:${robolectric_version}"){
                //Gotta coax it a little bit in order for Gradle to fetch its transitives.
                transitive = true
            },
            robolectric_shadows_supportv4: dependencies.create("org.robolectric:shadows-support-v4:${robolectric_version}"){
                exclude group: 'com.android.support', module: 'support-annotations'
            },
            robolectric_shadows_play_services: "org.robolectric:shadows-play-services:${robolectric_version}",
            dexmaker: dependencies.create("com.google.dexmaker:dexmaker:${dexmaker_version}"){
                //Stripping out current Hamcrest version and replacing with 1.3 declaration
                exclude group: 'org.hamcrest'
            },
            dexmaker_mockito: dependencies.create("com.google.dexmaker:dexmaker-mockito:${dexmaker_version}"){
                //Stripping out current Hamcrest version and replacing with 1.3 declaration
                exclude group: 'org.hamcrest'
                exclude group: 'org.mockito', module: 'mockito-core'
                exclude group: 'org.objenesis', module: 'objenesis'
            },
            mockito: dependencies.create("org.mockito:mockito-core:${mockito_version}"){
                //Stripping out current Hamcrest version and replacing with 1.3 declaration
                exclude group: 'org.hamcrest'
            },
            hamcrest_core: "org.hamcrest:hamcrest-core:${hamcrest_version}",
            hamcrest_library: "org.hamcrest:hamcrest-library:${hamcrest_version}",
            hamcrest_integration: "org.hamcrest:hamcrest-integration:${hamcrest_version}",
            mockwebserver: "com.squareup.okhttp3:mockwebserver:${okhttp_version}"
    ]

    mockitoLibs = [
            testLibs.mockito
    ]

    dexmakerLibs = [
            testLibs.dexmaker,
            testLibs.dexmaker_mockito,
            testLibs.hamcrest_core
    ]

    assertjLibs = [
            testLibs.assertj_android,
            testLibs.assertj_supportv4
    ]

    espressoLibs = [
            testLibs.espresso_core,
            testLibs.espresso_contrib,
            testLibs.espresso_intents
    ]

    forcedDependencies = [
            ext.libraries.supportv4,
            ext.libraries.googlePlayServices,
            ext.thirdPartyLibs.eventbus
    ]
}

allprojects {
    repositories {
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
